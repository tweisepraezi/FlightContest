; Inno Setup 5

[Code]
const DBCompatibelDigits = 1; // Number of digits for database compatibility
const DBVersion = '2.2.0';
const FCCompatibelDigits = 1; // Number of digits for application compatibility
const FCVersion = '1.2.4';
[Setup]
AppName=Flight Contest
AppVerName=Flight Contest 1.2.4
AppVersion=1.2.4
VersionInfoVersion=1.2.4
AppPublisher=Thomas Weise, Deutscher Präzisionsflug-Verein e.V.
AppPublisherURL=http://www.praeziflug.de
;AppUpdatesURL=https://github.com/tweisepraezi/FlightContest/downloads/FCSetup.exe
;AppSupportURL=http://

DefaultDirName={pf}\Flight Contest
DefaultGroupName=Flight Contest

OutputDir=..\output
OutputBaseFilename=FCSetup

LicenseFile=..\fc\web-app\licenses\GPL_License.txt
InfoBeforeFile=..\readme.txt
UninstallDisplayIcon={app}\fc.ico

DisableProgramGroupPage=yes
MinVersion=0,5

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl";
Name: "de"; MessagesFile: "German-2-5.1.11.isl";

[Messages]
en.BeveledLabel=Flight Contest
de.BeveledLabel=Flight Contest

[CustomMessages]
en.AdminNecessary=You need administrator privileges to install 'Flight Contest'.
de.AdminNecessary=Sie benötigen Adminstrator-Rechte zur Installation von 'Flight Contest'.
en.UninstallNecessary=Please deinstall 'Flight Contest' previously.
de.UninstallNecessary=Bitte deinstallieren Sie zuvor 'Flight Contest'.
en.BackupError=Backup error
de.BackupError=Backup-Fehler

[Files]
Source: "..\..\..\Java\jdk1.6.0_30\*";                DestDir: "{app}\java";            Flags: ignoreversion recursesubdirs;  Excludes: "doc,demo,include,sample,src.zip";
Source: "..\..\..\Server\apache-tomcat-7.0.25-x86\*"; DestDir: "{app}\tomcat";          Flags: ignoreversion recursesubdirs;  Excludes: "docs,examples";
Source: ".\fc-service.bat";                           DestDir: "{app}\tomcat\bin";
Source: ".\tomcat-users.xml";                         DestDir: "{app}\tomcat\conf";
Source: "..\output\fc.war";                           DestDir: "{app}\tomcat\webapps";
Source: "..\readme.txt";                              DestDir: "{app}";                 Attribs: readonly; Flags: overwritereadonly;
Source: ".\usage.rtf";                                DestDir: "{app}";                 Attribs: readonly; Flags: overwritereadonly;
Source: ".\tips.txt";                                 DestDir: "{app}";                 Attribs: readonly; Flags: overwritereadonly;
Source: ".\install-service.bat";                      DestDir: "{app}";
Source: ".\odbc.vbs";                                 DestDir: "{app}";
Source: "..\fc\web-app\images\fc.ico";                DestDir: "{app}";
Source: ".\FC-CrewList-Sample.xls";                   DestDir: "{app}\samples";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\FC-ObservationLog-Sample.xls";             DestDir: "{app}\samples";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\AFLOS.mdb";                                DestDir: "{app}\samples";         Attribs: readonly; Flags: overwritereadonly; DestName: "AFLOS-DemoContest.mdb"; 
Source: ".\AFLOS.mdb";                                DestDir: "C:\AFLOS\AFLOS_System";
Source: ".\rules\FAI_gac_precision_rules_2012.pdf";   DestDir: "{app}\rules";           Attribs: readonly; Flags: overwritereadonly;
Source: ".\rules\FAI_rally_flying_rules_2012.pdf";    DestDir: "{app}\rules";           Attribs: readonly; Flags: overwritereadonly;
Source: ".\rules\DE_Praezisionsflug_2005.pdf";        DestDir: "{app}\rules";           Attribs: readonly; Flags: overwritereadonly;
Source: ".\rules\DE_Navigationsflug_2012.pdf";        DestDir: "{app}\rules";           Attribs: readonly; Flags: overwritereadonly;
Source: ".\rules\CH_Praezisionsflug_2009.pdf";        DestDir: "{app}\rules";           Attribs: readonly; Flags: overwritereadonly;
Source: "..\fc\web-app\licenses\GPL_License.txt";     DestDir: "{app}";                 Attribs: readonly; Flags: overwritereadonly; AfterInstall: MyAfterInstall;

[Registry]
Root: HKLM32; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; ValueType: string; ValueName: "{app}\tomcat\bin\tomcat7w.exe"; ValueData: "RUNASADMIN"; Check: IsUAC32();
Root: HKLM64; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; ValueType: string; ValueName: "{app}\tomcat\bin\tomcat7w.exe"; ValueData: "RUNASADMIN"; Check: IsUAC64();

[Run]
Filename: "wscript.exe";                    Parameters: """{app}\odbc.vbs"" ""{app}""";    WorkingDir: "{app}";  Flags: runhidden;
Filename: "{app}\install-service.bat";      Parameters: "install FlightContest";           WorkingDir: "{app}";  Flags: runhidden;
Filename: "{app}\tomcat\bin\tomcat7w.exe";  Parameters: "//MS//FlightContest";             WorkingDir: "{app}";  Flags: nowait;

[UninstallRun]
Filename: "taskkill";                       Parameters: "/F /IM tomcat7w.exe";             WorkingDir: "{app}";
Filename: "{app}\install-service.bat";      Parameters: "remove FlightContest";            WorkingDir: "{app}";  Flags: runhidden;

[InstallDelete]
Type: filesandordirs; Name: "{app}\java";
Type: filesandordirs; Name: "{app}\tomcat";
Type: filesandordirs; Name: "{app}\samples";
Type: filesandordirs; Name: "{app}\rules";
Type: filesandordirs; Name: "{group}";

[UninstallDelete]
Type: filesandordirs; Name: "{app}\java";
Type: filesandordirs; Name: "{app}\tomcat";
Type: filesandordirs; Name: "{app}\samples";
Type: filesandordirs; Name: "{app}\rules";

[Icons]
Name: "{group}\Flight Contest Service Manager"; Filename: "{app}\tomcat\bin\tomcat7w.exe";          Parameters: "//MS//FlightContest";           WorkingDir: "{app}";
Name: "{group}\Flight Contest";                 Filename: "http://localhost:8080/fc/contest/start"; IconFilename: "{app}\fc.ico";                WorkingDir: "{commondesktop}";
Name: "{commondesktop}\Flight Contest";         Filename: "http://localhost:8080/fc/contest/start"; IconFilename: "{app}\fc.ico";                WorkingDir: "{commondesktop}";
Name: "{group}\Präzisionsflug-Verein e.V.";     Filename: "http://www.praeziflug.de";                                                            WorkingDir: "{commondesktop}";
Name: "{group}\Readme";                         Filename: "{app}\readme.txt";                                                                    WorkingDir: "{commondesktop}";
Name: "{group}\Tips";                           Filename: "{app}\tips.txt";                                                                      WorkingDir: "{commondesktop}";
Name: "{group}\Usage";                          Filename: "{app}\usage.rtf";                                                                     WorkingDir: "{commondesktop}";
Name: "{group}\Lizenz";                         Filename: "{app}\GPL_License.txt";                                                               WorkingDir: "{commondesktop}";
Name: "{group}\Install AFLOS connection";       Filename: "wscript.exe";                            Parameters: """{app}\odbc.vbs"" ""{app}""";  WorkingDir: "{app}";
Name: "{group}\Uninstall Flight Contest";                                            Filename: "{uninstallexe}";
Name: "{group}\Samples\FC-CrewList-Sample.xls";                                      Filename: "{app}\samples\FC-CrewList-Sample.xls";           WorkingDir: "{commondesktop}";
Name: "{group}\Samples\FC-ObservationLog-Sample.xls";                                Filename: "{app}\samples\FC-ObservationLog-Sample.xls";     WorkingDir: "{commondesktop}";
Name: "{group}\Rules\FAI Precision Flying Rules and Regulations 2012";               Filename: "{app}\rules\FAI_gac_precision_rules_2012.pdf";   WorkingDir: "{commondesktop}";
Name: "{group}\Rules\FAI Air Rally Flying Rules and Regulations 2012";               Filename: "{app}\rules\\FAI_rally_flying_rules_2012.pdf";   WorkingDir: "{commondesktop}";
Name: "{group}\Rules\Wettbewerbsregeln Präzisionsflug Deutschland 2005";             Filename: "{app}\rules\\DE_Praezisionsflug_2005.pdf";       WorkingDir: "{commondesktop}";
Name: "{group}\Rules\Wettbewerbsordnung Navigationsflug Deutschland 2012";           Filename: "{app}\rules\\DE_Navigationsflug_2012.pdf";       WorkingDir: "{commondesktop}";
Name: "{group}\Rules\Wettkampfreglement Präzisionsflug-Schweizermeisterschaft 2009"; Filename: "{app}\rules\\CH_Praezisionsflug_2009.pdf";       WorkingDir: "{commondesktop}";

[Code]
//---------------------------------------------------------------------------------------------------------
function GetCompareVersion(testVersion: string; compatibelDigits: integer): string;
var digit: integer;
    version: string;
    sub_version: string;
    new_version: string;
begin
  digit := 0;
  version := testVersion + '.';
  while ( (Pos('.', version) > 0) and (digit < compatibelDigits) ) do begin
    digit := digit + 1;
    sub_version := Copy(version, 1, Pos('.', version)-1);
    version := Copy(version, Pos('.', version)+1, Length(version));
    if (new_version = '') then begin
      new_version := sub_version;
    end else begin
      new_version := new_version + '.' + sub_version;
    end;
  end;
  Result := new_version;
end;

//---------------------------------------------------------------------------------------------------------
function IsCompatibel(lastVersion: string; actVersion: string; compatibelDigits: integer): Boolean;
begin
  if GetCompareVersion(actVersion,compatibelDigits) <> GetCompareVersion(lastVersion,compatibelDigits) then begin
    Result := false;
    exit;
  end;
  Result := true;
end;

//---------------------------------------------------------------------------------------------------------
function IsUAC32(): boolean;
var win_version: TWindowsVersion;
begin
  GetWindowsVersionEx(win_version);
  if win_version.Major = 6 then begin
    if not IsWin64() then begin
      Result := true;
      exit;
    end;
  end;
  Result := false;
end;

//---------------------------------------------------------------------------------------------------------
function IsUAC64(): boolean;
var win_version: TWindowsVersion;
begin
  GetWindowsVersionEx(win_version);
  if win_version.Major = 6 then begin
    if IsWin64() then begin
      Result := true;
      exit;
    end;
  end;
  Result := false;
end;

//---------------------------------------------------------------------------------------------------------
function InitializeSetup(): Boolean;
var last_version: string;
    shell: variant;
    ret: LongInt;
begin

  // Admin?
  if not IsAdminLoggedOn then begin
    MsgBox( ExpandConstant('{cm:AdminNecessary}'), mbInformation, MB_OK);
    Result := false;
    exit;
  end;

  // Uninstall?
  if RegQueryStringValue( HKLM, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Flight Contest_is1', 'DisplayVersion', last_version ) then begin
    if not IsCompatibel(last_version,FCVersion,FCCompatibelDigits) then begin
      MsgBox( ExpandConstant('{cm:UninstallNecessary}'), mbInformation, MB_OK);
      Result := false;
      exit;
    end;
  end;
  
  // save last version
  if not RegQueryStringValue( HKCU, 'SOFTWARE\Flight Contest', 'lastVersion', last_version ) then begin
    if RegQueryStringValue( HKLM, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Flight Contest_is1', 'DisplayVersion', last_version ) then begin
      RegWriteStringValue( HKCU, 'SOFTWARE\Flight Contest', 'lastVersion', last_version )
    end;
  end;

  // stop tomcat
  shell := CreateOleObject('WScript.Shell');
  ret := shell.Run('cmd.exe /Ctaskkill.exe /F /IM tomcat7w.exe', 0, true);  // 0 - unsichtbar, True - Wait for return
  ret := shell.Run('cmd.exe /Ctaskkill.exe /F /IM tomcat7.exe', 0, true);  // 0 - unsichtbar, True - Wait for return
  //MsgBox( IntToStr(ret), mbInformation, MB_OK);

  Result := true;
end;

//---------------------------------------------------------------------------------------------------------
procedure MyAfterInstall();
var last_version: string;
    db_dir: string;
    db_backup_dir: string;
    samples_dir: string;
    ret: integer;
begin

  // get last version
  RegQueryStringValue( HKCU, 'SOFTWARE\Flight Contest', 'lastVersion', last_version )
  
  // get db directory
  db_dir := ExpandConstant('{app}\fc');
  samples_dir := ExpandConstant('{app}\samples');
  
  // db backup
  if not IsCompatibel(last_version,DBVersion,DBCompatibelDigits) then begin
    if DirExists( db_dir ) then begin
      db_backup_dir := db_dir + '-' + GetDateTimeString('yyyy.mm.dd-hh.nn.ss', #0, #0);
      if last_version <> '' then begin
        db_backup_dir := db_backup_dir + '-' + last_version;
      end;
      RenameFile( db_dir, db_backup_dir );
    end;
  end else begin
    if DirExists( db_dir ) then begin
      db_backup_dir := db_dir + '-backup-' + GetDateTimeString('yyyy.mm.dd-hh.nn.ss', #0, #0);
      if last_version <> '' then begin
        db_backup_dir := db_backup_dir + '-' + last_version;
      end;
      if CreateDir(db_backup_dir) then begin
        Exec('xcopy', '/s "' + db_dir + '" "' + db_backup_dir + '"', ExpandConstant('{app}'), SW_HIDE, ewWaitUntilTerminated, ret );
        if ret <> 0 then begin
          MsgBox( ExpandConstant('{cm:BackupError}') + ' ' + IntToStr(ret), mbInformation, MB_OK);
        end;
      end;
    end;
  end;
  
  // create db directory
  if not DirExists( db_dir ) then begin
    CreateDir( db_dir );
  end;
  
  // create AFLOS-UPLOAD.mdb
  if not FileExists( db_dir + '\AFLOS-UPLOAD.mdb' ) then begin
    FileCopy(samples_dir + '\AFLOS-DemoContest.mdb', db_dir + '\AFLOS-UPLOAD.mdb', true);
  end;

  // Remove logs
  DelTree( db_dir + '\logs', true, true, true );
  DeleteFile( db_dir + '\fcdb.trace.db' );

  // write installed version
  RegWriteStringValue( HKCU, 'SOFTWARE\Flight Contest', 'lastVersion', DBVersion )

end;
