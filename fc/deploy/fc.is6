; Inno Setup 6.1

[Code]
const DBCompatibelDigits = 1;   // Number of digits for database compatibility
const DBVersion = '2.36.0';     // DB-2.36
const FCCompatibelDigits = 0;   // Number of digits for application compatibility
const FCVersion = '3.3.2';      // no milestone
const FCCompatibelDigits64 = 2; // Number of digits for application compatibility
const FCVersion64 = '3.3.2';    // no milestone
[Setup]
AppName=Flight Contest

AppVerName=Flight Contest 3.3.2
AppVersion=3.3.2
; no milestone below
VersionInfoVersion=3.3.2

AppCopyright=(C) 2009-2023 Thomas Weise, Deutscher Präzisionsflug-Verein e.V.
AppPublisher=Thomas Weise, Deutscher Präzisionsflug-Verein e.V.
AppPublisherURL=http://flightcontest.de
;AppUpdatesURL=http://flightcontest.de
AppSupportURL=http://flightcontest.de

DefaultDirName={commonpf}\Flight Contest
DefaultGroupName=Flight Contest

OutputDir=..\output
OutputBaseFilename=FCSetup

LicenseFile=..\web-app\licenses\GPL_License.txt
InfoBeforeFile=.\readme.txt
UninstallDisplayIcon={app}\fc.ico

DisableProgramGroupPage=yes
MinVersion=0,7
ArchitecturesInstallIn64BitMode=x64
PrivilegesRequired=admin

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl";
Name: "de"; MessagesFile: "German-6.1.isl";

[Messages]
;en.BeveledLabel=Flight Contest
;de.BeveledLabel=Flight Contest

[CustomMessages]
en.AdminNecessary=You need administrator privileges to install 'Flight Contest'.
de.AdminNecessary=Sie benötigen Adminstrator-Rechte zur Installation von 'Flight Contest'.
en.UninstallNecessary=Please deinstall 'Flight Contest' previously.
de.UninstallNecessary=Bitte deinstallieren Sie zuvor 'Flight Contest'.
en.BackupError=Backup error
de.BackupError=Backup-Fehler

[Files]
Source: "..\..\..\..\Java\openjdk-1.8.0.302x64\jre\*";                DestDir: "{app}\java";       Flags: ignoreversion recursesubdirs;  Excludes: "doc,demo,include,sample,src.zip";
Source: "..\..\..\..\Server\apache-tomcat-9.0.54-windows-x64\*";      DestDir: "{app}\tomcat";     Flags: ignoreversion recursesubdirs;  Excludes: "docs,examples";
Source: "..\..\..\..\Server\apache-tomcat-9.0.13-windows-x64\bin\tomcat9.exe";  DestDir: "{app}\tomcat\bin"; Flags: ignoreversion recursesubdirs;  Excludes: "docs,examples";
Source: "..\..\..\..\Server\apache-tomcat-9.0.13-windows-x64\bin\tomcat9w.exe"; DestDir: "{app}\tomcat\bin"; Flags: ignoreversion recursesubdirs;  Excludes: "docs,examples";
;Source: "..\..\..\..\gdal\gdal-3.2.1\bin\*.dll";                      DestDir: "{app}\tomcat\bin";
;Source: "..\..\..\..\gdal\gdal-3.2.1\bin\gdal\java\gdalalljni.dll";   DestDir: "{app}\tomcat\bin";
Source: ".\setenv.bat";                                               DestDir: "{app}\tomcat\bin";
;Source: "..\..\..\..\gdal\gdal-3.2.1\bin\*.dll";                      DestDir: "{app}\gdal\bin";
;Source: "..\..\..\..\gdal\gdal-3.2.1\bin\gdal\java\gdalalljni.dll";   DestDir: "{app}\gdal\bin";
;Source: "..\..\..\..\gdal\gdal-3.2.1\bin\proj7\share\proj.db";        DestDir: "{app}\gdal";
;Source: "..\..\..\..\gdal\gdal-3.2.1\bin\proj7\share\proj.ini";       DestDir: "{app}\gdal";
Source: "..\output\fc.war";                           DestDir: "{app}\tomcat\webapps";
Source: ".\readme.txt";                               DestDir: "{app}";                 Attribs: readonly; Flags: overwritereadonly uninsremovereadonly;
Source: ".\install-service.bat";                      DestDir: "{app}";
Source: "..\web-app\images\fc.ico";                   DestDir: "{app}";
Source: ".\save_fcdb.bat";                            DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\save_fcsave.bat";                          DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\start_fc.bat";                             DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\stop_fc.bat";                              DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\FCAutoOpenPDF.vbs";                        DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\FCAutoLoadScan_PlanningTask.vbs";          DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\FCAutoLoadScan_Observation.vbs";           DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\FCAutoLoad_Logger.vbs";                    DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\read_skytraq_logger.bat";                  DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\read_dg100_logger.bat";                    DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: ".\read_dg200_logger.bat";                    DestDir: "{app}\scripts";         Attribs: readonly; Flags: overwritereadonly;
Source: "..\web-app\licenses\GPL_License.txt";        DestDir: "{app}";                 Attribs: readonly; Flags: overwritereadonly uninsremovereadonly; AfterInstall: MyAfterInstall;

;Source: ".\tomcat-users.xml";                         DestDir: "{app}\tomcat\conf";
;Source: ".\fc-service-start.xml";                     DestDir: "{app}";

[Dirs]
Name: "C:\FCSave";         Flags: uninsneveruninstall; Permissions: everyone-modify;
Name: "C:\FCSave\.fc";     Flags: uninsneveruninstall; Permissions: everyone-modify;
Name: "C:\FCSave\.scan";   Flags: uninsneveruninstall; Permissions: everyone-modify;
Name: "C:\FCSave\.logger"; Flags: uninsneveruninstall; Permissions: everyone-modify;
Name: "C:\FCSave\.geodata";Flags: uninsneveruninstall; Permissions: everyone-modify;
Name: "C:\FCSave\.geodata\images";Flags: uninsneveruninstall; Permissions: everyone-modify;

[Registry]
;Root: HKLM32; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; ValueType: string; ValueName: "{app}\tomcat\bin\tomcat9w.exe"; ValueData: "RUNASADMIN"; Check: IsUAC32();
Root: HKLM64; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; ValueType: string; ValueName: "{app}\tomcat\bin\tomcat9w.exe"; ValueData: "RUNASADMIN"; Check: IsUAC64();

[Run]
; install FlightContest service
Filename: "{app}\install-service.bat";      Parameters: "install FlightContest";           WorkingDir: "{app}"; Flags: runhidden;
Filename: "sc.exe";                         Parameters: "sdset FlightContest D:AR(A;;CCDCLCSWRPWPDTLOCRSDRCWDWO;;;SY)(A;;CCDCLCSWRPWPDTLOCRSDRCWDWO;;;BA)(A;;CCLCSWRPWPDTLOCRRC;;;IU)S:(AU;FA;CCDCLCSWRPWPDTLOCRSDRCWDWO;;;WD)"; WorkingDir: "{app}";  Flags: runhidden;
; start tomcat monitor
Filename: "{app}\tomcat\bin\tomcat9w.exe";  Parameters: "//MS//FlightContest";             WorkingDir: "{app}"; Flags: nowait;
;Filename: "schtasks.exe";                   Parameters: "/Create /S {computername} /XML ""{app}\fc-service-start.xml"" /TN ""Flight Contest Service Manager"""; WorkingDir: "{app}";  Flags: runhidden;

[UninstallRun]
; remove FlightContest service
Filename: "{app}\install-service.bat";      Parameters: "remove FlightContest";            WorkingDir: "{app}"; Flags: runhidden; RunOnceId: "RemoveFCService";

[InstallDelete]
Type: filesandordirs; Name: "{app}\java";
Type: filesandordirs; Name: "{app}\tomcat";
Type: filesandordirs; Name: "{app}\gdal";
Type: filesandordirs; Name: "{app}\samples";
Type: filesandordirs; Name: "{app}\scripts";
Type: filesandordirs; Name: "{app}\rules";
Type: filesandordirs; Name: "{group}";

[UninstallDelete]
Type: filesandordirs; Name: "{app}\java";
Type: filesandordirs; Name: "{app}\tomcat";
Type: filesandordirs; Name: "{app}\gdal";
Type: filesandordirs; Name: "{app}\samples";
Type: filesandordirs; Name: "{app}\scripts";
Type: filesandordirs; Name: "{app}\rules";

[Icons]
;Name: "{userdesktop}\Flight Contest";           Filename: "http://localhost:8080/fc/contest/start"; IconFilename: "{app}\fc.ico";                WorkingDir: "{userdesktop}";    
  ;nur sichtbar, wenn User zur Administrator-Gruppe gehört
Name: "{commondesktop}\Flight Contest";         Filename: "http://localhost:8080/fc/contest/start"; IconFilename: "{app}\fc.ico";                WorkingDir: "{commondesktop}";    
Name: "{group}\Flight Contest Service Manager"; Filename: "{app}\tomcat\bin\tomcat9w.exe";          Parameters: "//MS//FlightContest";           WorkingDir: "{app}";
Name: "{group}\Flight Contest";                 Filename: "http://localhost:8080/fc/contest/start"; IconFilename: "{app}\fc.ico";                WorkingDir: "{commondesktop}";
Name: "{group}\flightcontest.de";               Filename: "http://flightcontest.de";                                                             WorkingDir: "{commondesktop}";
Name: "{group}\Readme";                         Filename: "{app}\readme.txt";                                                                    WorkingDir: "{commondesktop}";
Name: "{group}\Hilfe";                          Filename: "{app}\tomcat\webapps\fc\docs\help_de.html";                                           WorkingDir: "{commondesktop}";
Name: "{group}\Help";                           Filename: "{app}\tomcat\webapps\fc\docs\help_en.html";                                           WorkingDir: "{commondesktop}";
Name: "{group}\Bedienungsanleitung";            Filename: "{app}\tomcat\webapps\fc\docs\manuals\FC-Manual.pdf";                                  WorkingDir: "{commondesktop}";
Name: "{group}\Manual";                         Filename: "{app}\tomcat\webapps\fc\docs\manuals\FC-Manual_en.pdf";                               WorkingDir: "{commondesktop}";
Name: "{group}\Anleitung Beobachtungen";        Filename: "{app}\tomcat\webapps\fc\docs\manuals\FC-Manual-Observations.pdf";                     WorkingDir: "{commondesktop}";
Name: "{group}\Planungstest-Ablauf";            Filename: "{app}\tomcat\webapps\fc\docs\info\FC-PlanningTest-Info.docx";                         WorkingDir: "{commondesktop}";
Name: "{group}\Planning Test schedule";         Filename: "{app}\tomcat\webapps\fc\docs\info\FC-PlanningTest-Info_en.docx";                      WorkingDir: "{commondesktop}";
Name: "{group}\Lizenz";                         Filename: "{app}\GPL_License.txt";                                                               WorkingDir: "{commondesktop}";
Name: "{group}\Uninstall Flight Contest";                                            Filename: "{uninstallexe}";
Name: "{group}\Scripts\Save contest database";                                       Filename: "{app}\scripts\save_fcdb.bat";                    WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\Save saved files";                                            Filename: "{app}\scripts\save_fcsave.bat";                  WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\Start Flight Contest";                                        Filename: "{app}\scripts\start_fc.bat";                     WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\Stop Flight Contest";                                         Filename: "{app}\scripts\stop_fc.bat";                      WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\FCAutoOpenPDF";                                               Filename: "{app}\scripts\FCAutoOpenPDF.vbs";                WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\FCAutoLoadScanPlanningTask";                                  Filename: "{app}\scripts\FCAutoLoadScan_PlanningTask.vbs";  WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\FCAutoLoadScanObservation";                                   Filename: "{app}\scripts\FCAutoLoadScan_Observation.vbs";   WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\FCAutoLoadLogger";                                            Filename: "{app}\scripts\FCAutoLoad_Logger.vbs";            WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\Read SkyTraq logger Renkforce GT-730FL-S";                    Filename: "{app}\scripts\read_skytraq_logger.bat";          WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\Read logger GlobalSat DG-100";                                Filename: "{app}\scripts\read_dg100_logger.bat";            WorkingDir: "{commondesktop}";
Name: "{group}\Scripts\Read logger GlobalSat DG-200";                                Filename: "{app}\scripts\read_dg200_logger.bat";            WorkingDir: "{commondesktop}";
Name: "{group}\Samples\FC-CrewList-Sample.xlsx";                                     Filename: "{app}\tomcat\webapps\fc\docs\samples\FC-CrewList-Sample.xlsx";          WorkingDir: "{commondesktop}";
Name: "{group}\Samples\FC-TakoffLandingCalculation-Sample.xlsx";                     Filename: "{app}\tomcat\webapps\fc\docs\samples\FC-TakoffLandingCalculation-Sample.xlsx"; WorkingDir: "{commondesktop}";
Name: "{group}\Rules\FAI Precision Flying Rules and Regulations 2022";               Filename: "{app}\tomcat\webapps\fc\docs\rules\FAI_precision_flying_rules_2022.pdf";WorkingDir: "{commondesktop}";
Name: "{group}\Rules\FAI Air Rally Flying Rules and Regulations 2022";               Filename: "{app}\tomcat\webapps\fc\docs\rules\FAI_rally_flying_rules_2022.pdf";    WorkingDir: "{commondesktop}";
Name: "{group}\Rules\FAI GAC Landing appendix 2022";                                 Filename: "{app}\tomcat\webapps\fc\docs\rules\FAI_landing_rules_2022.pdf";         WorkingDir: "{commondesktop}";
Name: "{group}\Rules\Wettbewerbsordnung Navigationsflug Deutschland 2020";           Filename: "{app}\tomcat\webapps\fc\docs\rules\DE_Navigationsflug_2020.pdf";        WorkingDir: "{commondesktop}";
Name: "{group}\Rules\Wettbewerbsordnung Navigationsflug Deutschland 2017";           Filename: "{app}\tomcat\webapps\fc\docs\rules\DE_Navigationsflug_2017.pdf";        WorkingDir: "{commondesktop}";
Name: "{group}\Rules\Wettkampfreglement Präzisionsflug-Schweizermeisterschaft 2017"; Filename: "{app}\tomcat\webapps\fc\docs\rules\CH_Praezisionsflug_2017.pdf";        WorkingDir: "{commondesktop}";
Name: "{group}\Rules\Motorflug-Wettbewerbsordnung Österreich 2017";                  Filename: "{app}\tomcat\webapps\fc\docs\rules\AT_Motorflug_2017.pdf";              WorkingDir: "{commondesktop}";

[Code]
//---------------------------------------------------------------------------------------------------------
function GetCompareVersion(testVersion: string; compatibelDigits: integer): string;
var digit: integer;
    version: string;
    sub_version: string;
    new_version: string;
begin
  digit := 0;
  version := testVersion + '.';
  while ( (Pos('.', version) > 0) and (digit < compatibelDigits) ) do begin
    digit := digit + 1;
    sub_version := Copy(version, 1, Pos('.', version)-1);
    version := Copy(version, Pos('.', version)+1, Length(version));
    if (new_version = '') then begin
      new_version := sub_version;
    end else begin
      new_version := new_version + '.' + sub_version;
    end;
  end;
  Result := new_version;
end;

//---------------------------------------------------------------------------------------------------------
function IsCompatibel(lastVersion: string; actVersion: string; compatibelDigits: integer): Boolean;
begin
  if GetCompareVersion(actVersion,compatibelDigits) <> GetCompareVersion(lastVersion,compatibelDigits) then begin
    Result := false;
    exit;
  end;
  Result := true;
end;

//---------------------------------------------------------------------------------------------------------
function IsUAC32(): boolean;
var win_version: TWindowsVersion;
begin
  GetWindowsVersionEx(win_version);
  if win_version.Major = 6 then begin
    if not IsWin64() then begin
      Result := true;
      exit;
    end;
  end;
  Result := false;
end;

//---------------------------------------------------------------------------------------------------------
function IsUAC64(): boolean;
var win_version: TWindowsVersion;
begin
  GetWindowsVersionEx(win_version);
  if win_version.Major = 6 then begin
    if IsWin64() then begin
      Result := true;
      exit;
    end;
  end;
  Result := false;
end;

//---------------------------------------------------------------------------------------------------------
function InitializeSetup(): Boolean;
var last_version: string;
    shell: variant;
    ret: LongInt;
begin

  // Admin?
  if not IsAdminInstallMode then begin
    MsgBox( ExpandConstant('{cm:AdminNecessary}'), mbInformation, MB_OK);
    Result := false;
    exit;
  end;

  // Uninstall x64?
  if RegQueryStringValue( HKLM, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Flight Contest_is1', 'DisplayVersion', last_version ) then begin
    if GetCompareVersion(last_version,1) = '0' then begin
      MsgBox( ExpandConstant('{cm:UninstallNecessary}'), mbInformation, MB_OK);
      Result := false;
      exit;
    end;  
    if not IsCompatibel(last_version,FCVersion,FCCompatibelDigits) then begin
      MsgBox( ExpandConstant('{cm:UninstallNecessary}'), mbInformation, MB_OK);
      Result := false;
      exit;
    end;
  end;
  
  // Uninstall x86?
  if RegQueryStringValue( HKLM, 'SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\Flight Contest_is1', 'DisplayVersion', last_version ) then begin
    if not IsCompatibel(last_version,FCVersion64,FCCompatibelDigits64) then begin
      MsgBox( ExpandConstant('{cm:UninstallNecessary}'), mbInformation, MB_OK);
      Result := false;
      exit;
    end;
  end;

  // stop tomcat
  shell := CreateOleObject('WScript.Shell');
  ret := shell.Run('cmd.exe /Ctaskkill.exe /F /IM tomcat9w.exe', 0, true);  // 0 - unsichtbar, True - Wait for return
  ret := shell.Run('cmd.exe /Ctaskkill.exe /F /IM tomcat9.exe', 0, true);  // 0 - unsichtbar, True - Wait for return
  //MsgBox( IntToStr(ret), mbInformation, MB_OK);

  Result := true;
end;

//---------------------------------------------------------------------------------------------------------
procedure MyAfterInstall();
var last_version: string;
    db_dir: string;
    db_backup_dir: string;
    // fc_service_start_filename: string;
    ret: integer;
    //fc_service_start_array: TArrayOfString;
    //user_domain: string;
    //i: integer;
    //p: integer;
begin

  // get last version
  RegQueryStringValue( HKCU, 'SOFTWARE\Flight Contest', 'lastVersion', last_version )
  
  // get db directory
  db_dir := ExpandConstant('{app}\fc');
  // fc_service_start_filename := ExpandConstant('{app}\fc-service-start.xml');
  
  // db backup
  if DirExists( db_dir ) then begin
    db_backup_dir := db_dir + '-backup-' + GetDateTimeString('yyyy.mm.dd-hh.nn.ss', #0, #0);
    if last_version <> '' then begin
      db_backup_dir := db_backup_dir + '-' + last_version;
    end;
    if CreateDir(db_backup_dir) then begin
      Exec('xcopy', '/s "' + db_dir + '" "' + db_backup_dir + '"', ExpandConstant('{app}'), SW_HIDE, ewWaitUntilTerminated, ret );
      if ret <> 0 then begin
        MsgBox( ExpandConstant('{cm:BackupError}') + ' ' + IntToStr(ret), mbInformation, MB_OK);
      end;
    end;
  end;
  
  // create db directory
  if not DirExists( db_dir ) then begin
    CreateDir( db_dir );
  end;
  
  // modify task
  //if LoadStringsFromFile(fc_service_start_filename, fc_service_start_array) then begin
  //  user_domain :=  ExpandConstant('{%USERDOMAIN}')
  //  for i := 0 to GetArrayLength(fc_service_start_array)-1 do begin
  //    StringChangeEx(fc_service_start_array[i],'${USERID}', user_domain + '\' + ExpandConstant('{username}'), true);
  //    StringChangeEx(fc_service_start_array[i],'${CMD}',    ExpandConstant('{app}\tomcat\bin\tomcat9w.exe'), true);
  //  end;
  //  SaveStringsToFile(fc_service_start_filename, fc_service_start_array, false); // false - no append
  //end;

  // Remove logs
  DelTree( db_dir + '\logs', true, true, true );
  DeleteFile( db_dir + '\fcdb.trace.db' );

  // Remove db lock
  DeleteFile( db_dir + '\fcdb.lock.db' );

  // write installed version
  RegWriteStringValue( HKCU, 'SOFTWARE\Flight Contest', 'lastVersion', DBVersion )

end;
